/batch

! JANCAE UMMDp Simple Example
! 3D Solid/Disp. Control
! Date : 2019-12-30

/prep7

! Element Type

et,1,185

! Material Property
! material property(ummdp)
tb,user,1,,26
tbdata, 1,  0           ! nela
tbdata, 2,  1000.0      ! E
tbdata, 3,  0.3         ! nu
tbdata, 4,  2           ! nyld (Yld2004Åj
tbdata, 5, -0.069888    ! c'12
tbdata, 6,  0.936408    ! c'13
tbdata, 7,  0.079143    ! c'21
tbdata, 8,  1.003060    ! c'23
tbdata, 9,  0.524741    ! c'31
tbdata, 10, 1.363180    ! c'32
tbdata, 11, 0.954322    ! c'44
tbdata, 12, 1.023770    ! c'55
tbdata, 13, 1.069060    ! c"66
tbdata, 14, 0.981171    ! c"12
tbdata, 15, 0.476741    ! c"13
tbdata, 16, 0.575316    ! c"21
tbdata, 17, 0.866827    ! c"23
tbdata, 18, 1.145010    ! c"31
tbdata, 19,-0.079294    ! c"32
tbdata, 20, 1.404620    ! c"44
tbdata, 21, 1.051660    ! c"55
tbdata, 22, 1.147100    ! c"66
tbdata, 23, 8.0         ! exponent a 
tbdata, 24, 0           ! nih constant yield stress
tbdata, 25, 1.0         ! yield stress
tbdata, 26, 0           ! nkin (must be 0)

tb,state,1,,8

! Create Element

block,0,1,0,1,0,1
esize,,1
vmesh,1

! Boundary Condition

nsel,s,loc,x,0
d,all,ux
nsel,s,loc,y,0
d,all,uy
nsel,s,loc,z,0
d,all,uz
nsel,all

finish

/solu

! Set Solution Options

nlgeom,on
nsubst,50,50,50
outres,all,all

! Define Parameter

r = 5.0e-3

! Step 1 UX=+R/UY=0
nsel,s,loc,x,1.0
d,all,ux,r
nsel,s,loc,y,1.0
d,all,uy,0
nsel,all
solve

! Step 2 UX=+2R/UY=+R
nsel,s,loc,x,1.0
d,all,ux,2*r
nsel,s,loc,y,1.0
d,all,uy,r
nsel,all
solve

! Step 3 UX=+2R/UY=+2R
nsel,s,loc,x,1.0
d,all,ux,2*r
nsel,s,loc,y,1.0
d,all,uy,2*r
nsel,all
solve

! Step 4 UX=+R/UY=+3R
nsel,s,loc,x,1.0
d,all,ux,r
nsel,s,loc,y,1.0
d,all,uy,3*r
nsel,all
solve

! Step 5 UX=0/UY=+3R
nsel,s,loc,x,1.0
d,all,ux,0
nsel,s,loc,y,1.0
d,all,uy,3*r
nsel,all
solve

! Step 6 UX=-R/UY=+2R
nsel,s,loc,x,1.0
d,all,ux,-1*r
nsel,s,loc,y,1.0
d,all,uy,2*r
nsel,all
solve


! Step 7 UX=-R/UY=+R
nsel,s,loc,x,1.0
d,all,ux,-1*r
nsel,s,loc,y,1.0
d,all,uy,r
nsel,all
solve

! Step 8 UX=0/UY=0
nsel,s,loc,x,1.0
d,all,ux,0
nsel,s,loc,y,1.0
d,all,uy,0
nsel,all
solve

! Step 9 UX=R/UY=0
nsel,s,loc,x,1.0
d,all,ux,r
nsel,s,loc,y,1.0
d,all,uy,0
nsel,all
solve

finish

! Output Results

/post26

esol,2,1,,s,x,SX
esol,3,1,,s,y,SY
esol,4,1,,s,z,SZ
esol,5,1,,s,eqv,SEQV
esol,6,1,,nl,epeq,EPEQ
add,7,2,4,,SX-SZ,,,1,-1
add,8,3,4,,SY-SZ,,,1,-1

*dim,value,array,450,8
vget,value(1,1),1
vget,value(1,2),2
vget,value(1,3),3
vget,value(1,4),4
vget,value(1,5),5
vget,value(1,6),6
vget,value(1,7),7
vget,value(1,8),8

/output,UMMDp_ansys_solid1,csv
*vwrite
('TIME,s11(UMMDp),s22(UMMDp),s33(UMMDp),se(UMMDp),p(UMMDp),s11-s33/MPa,s22-s33/MPa')
*vwrite,value(1,1),value(1,2),value(1,3),value(1,4),value(1,5),value(1,6),value(1,7),value(1,8)
(e15.8,' , ',e15.8,' , ',e15.8,' , ',e15.8,' , ',e15.8,' , ',e15.8,' , ',e15.8,' , ',e15.8)
/output

finish
/exit
